import args.*;
import system.*;

type Args {
    @required()
    @description("File to read and print")
    file: String
}

fn cat_file(filename: String) -> number {
    let path = path_from_string(filename);

    if (file_exists(path) == false) {
        println("cat: ${filename}: No such file or directory");
        return 1;
    }
    let result = read_file(path);
    match (result) {
        case!ok content -> {
            print(content);
            return 0;
        },
        case!err err -> {
            println("cat: ${filename}: ${err->message}");
            return 1;
        }
    }
}

fn main(args: [String]) -> number {
    let parsed = parse_args<Args>(args);
    return match (parsed) {
        case!ok opts -> {
            return cat_file(opts->file);
        },
        case!err err -> {
            let help = usage<Args>("cat");
            if (err->message == "--help") {
                println(help);
                return 0;
            }
            println("Error: ${err->message}");
            println("");
            println(help);
            return 1;
        }
    };
}
