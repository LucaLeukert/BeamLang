type Iterator<T> {
    data: any,
    next: fn(Iterator<T>) -> Optional<T>,
    map: fn(Iterator<T>, fn(T) -> any) -> Iterator<any>,
    filter: fn(Iterator<T>, fn(T) -> bool) -> Iterator<T>,
    fold: fn(Iterator<T>, any, fn(any, T) -> any) -> any
}

type Optional<T> {
}

type Result<Ok, Err> {
}

@external(elixir, "BeamLang.Runtime", "println")
fn println(value: String) -> void;

@external(elixir, "BeamLang.Runtime", "print")
fn print(value: String) -> void;

@external(elixir, "BeamLang.Runtime", "typeof")
fn typeof(value: any) -> String;

@external(elixir, "BeamLang.Runtime", "string_length")
fn string_length(value: String) -> number;

@external(elixir, "BeamLang.Runtime", "string_concat")
fn string_concat(left: String, right: String) -> String;

@external(elixir, "BeamLang.Runtime", "string_chars_data")
fn string_chars_data(value: String) -> any;

@external(elixir, "BeamLang.Runtime", "iterator_next_data")
fn iterator_next_data(data: any) -> Optional<any>;

@external(elixir, "BeamLang.Runtime", "iterator_map_data")
fn iterator_map_data(data: any, mapper: fn(any) -> any) -> any;

@external(elixir, "BeamLang.Runtime", "iterator_filter_data")
fn iterator_filter_data(data: any, predicate: fn(any) -> bool) -> any;

@external(elixir, "BeamLang.Runtime", "iterator_fold_data")
fn iterator_fold_data(data: any, initial: any, folder: fn(any, any) -> any) -> any;

fn iterator_from_list(data: any) -> Iterator<any> {
    return { data = data, next = iterator_next, map = iterator_map, filter = iterator_filter, fold = iterator_fold };
}

fn iterator_next(it: Iterator<any>) -> Optional<any> {
    return iterator_next_data(it->data);
}

fn iterator_map(it: Iterator<any>, mapper: fn(any) -> any) -> Iterator<any> {
    let mapped = iterator_map_data(it->data, mapper);
    return iterator_from_list(mapped);
}

fn iterator_filter(it: Iterator<any>, predicate: fn(any) -> bool) -> Iterator<any> {
    let filtered = iterator_filter_data(it->data, predicate);
    return iterator_from_list(filtered);
}

fn iterator_fold(it: Iterator<any>, initial: any, folder: fn(any, any) -> any) -> any {
    return iterator_fold_data(it->data, initial, folder);
}

fn string_chars(value: String) -> Iterator<char> {
    let data = string_chars_data(value);
    return iterator_from_list(data);
}

fn optional_unwrap(value: Optional<any>, fallback: any) -> any {
    return match (value) {
        case?some inner => inner,
        case?none => fallback
    };
}

fn optional_map(value: Optional<any>, mapper: fn(any) -> any) -> Optional<any> {
    return match (value) {
        case?some inner => ?some mapper(inner),
        case?none => ?none
    };
}

fn optional_and_then(value: Optional<any>, mapper: fn(any) -> Optional<any>) -> Optional<any> {
    return match (value) {
        case?some inner => mapper(inner),
        case?none => ?none
    };
}

fn result_unwrap(value: Result<any, any>, fallback: any) -> any {
    return match (value) {
        case!ok inner => inner,
        case!err _ => fallback
    };
}

fn result_map(value: Result<any, any>, mapper: fn(any) -> any) -> Result<any, any> {
    return match (value) {
        case!ok inner => !ok mapper(inner),
        case!err err => !err err
    };
}

fn result_and_then(value: Result<any, any>, mapper: fn(any) -> Result<any, any>) -> Result<any, any> {
    return match (value) {
        case!ok inner => mapper(inner),
        case!err err => !err err
    };
}
